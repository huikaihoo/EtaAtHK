apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def versionMajor = 0
def versionMinor = 6
def versionPatch = 6
def versionBuild = 8

def static getCurrentDate() {
    def date = new Date()
    return date.format('yyyyMMdd')
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

kapt {
    useBuildCache = true
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "hoo.etahk"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode versionBuild
        versionName "$versionMajor.$versionMinor.$versionPatch." + getCurrentDate() + " ($versionBuild)"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.resValue 'string', 'google_maps_key', google_maps_api_key
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    //implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.5'

    // Android Core
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    // Google Service
    implementation 'com.google.android.gms:play-services-maps:15.0.1'

    // ViewModel + LiveData + Room + Paging
    implementation "android.arch.lifecycle:extensions:1.1.1"
    kapt "android.arch.lifecycle:compiler:1.1.1"
    implementation "android.arch.persistence.room:runtime:1.1.1"
    kapt "android.arch.persistence.room:compiler:1.1.1"
    implementation "android.arch.paging:runtime:1.0.1"

    // Kotlin library for android
    implementation 'com.mcxiaoke.koi:core:0.5.5'
    implementation 'androidx.core:core-ktx:0.3'

    // RecyclerView-FastScroll
    implementation 'com.github.huikaihoo:recyclerview-fastscroll:1.0.17-fork.1'
    //implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.18'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.4'

    // Retrofit2 (OkHttp3)
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    //implementation 'com.squareup.okhttp3:okhttp:3.9.1'

    // Debug
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.3'

    // Testing
    testImplementation 'android.arch.core:core-testing:1.1.1'
    testImplementation 'android.arch.persistence.room:testing:1.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

task copyFiles(type: Copy) {
    from 'src/main/res/values-zh/'
    include 'strings.xml'
    into 'src/main/res/values-zh-rHK/'
}

preBuild.dependsOn(copyFiles)
preBuild.mustRunAfter(copyFiles)
