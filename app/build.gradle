apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'de.undercouch.download'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionSuffix = "-alpha.1"

def versionBuild = 8

def static getBuildTime() {
    def date = new Date()
    return date.format('yyyyMMdd')
}

kapt {
    useBuildCache = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "hoo.etahk"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode versionBuild
        versionName "$versionMajor.$versionMinor.$versionPatch$versionSuffix"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "zh", "zh-rHK", "en"
        vectorDrawables.useSupportLibrary = true
        archivesBaseName = "$applicationId-$versionName"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.resValue 'string', 'google_maps_key', google_maps_api_key
    }
    lintOptions {
        check 'Interoperability'
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    applicationVariants.all { variant ->
        def flavor = variant.mergedFlavor
        def versionName = flavor.getVersionName()
        if (variant.buildType.isDebuggable()) {
            versionName = "$versionMajor.$versionMinor.$versionPatch-dev." + getBuildTime()
        }
        versionName += " ($versionBuild)"
        variant.outputs.each { output ->
            output.versionNameOverride = versionName
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    //implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.0'

    // Anko
    implementation "org.jetbrains.anko:anko-commons:$anko_version"

    // Android X Core + Material Design
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'

    // Google Service
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-places:16.0.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:16.0.1'

    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.google.firebase:firebase-config:16.1.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'

    // ViewModel + LiveData + Room + Paging
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'
    implementation 'androidx.room:room-runtime:2.1.0-alpha03'
    kapt 'androidx.room:room-compiler:2.1.0-alpha03'
    implementation 'androidx.paging:paging-runtime:2.1.0-rc01'

    // Kotlin library for android
    implementation 'androidx.core:core-ktx:1.0.1'
    implementation 'com.mcxiaoke.koi:core:0.5.5'

    // RecyclerView-FastScroll
    implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.20'
    //implementation 'com.github.huikaihoo:recyclerview-fastscroll:1.0.17-fork.1'

    // JSON / HTML Parser
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.jsoup:jsoup:1.11.3'

    // Retrofit2 (OkHttp3)
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    //implementation 'com.squareup.okhttp3:okhttp:3.12.1'

    // Debug
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4'

    // Unit Test
    testImplementation 'androidx.arch.core:core-testing:2.0.0'
    testImplementation 'androidx.room:room-testing:2.1.0-alpha03'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.1'

    // Android Unit Test
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

//task downloadFiles(type: Download) {
//    src 'https://raw.githubusercontent.com/google/diff-match-patch/master/java/src/name/fraser/neil/plaintext/diff_match_patch.java'
//    dest 'src/main/java/name/fraser/neil/plaintext/diff_match_patch.java'
//}

task copyFiles(type: Copy) {
    from 'src/main/res/values-zh/'
    include 'strings.xml'
    into 'src/main/res/values-zh-rHK/'
}

preBuild.dependsOn(copyFiles)
preBuild.mustRunAfter(copyFiles)
